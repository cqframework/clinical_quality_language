apply plugin: 'application'
apply plugin: 'antlr'

mainClassName = 'org.cqframework.cql.Main'
run.args = ["${projectDir}/../../../Examples/ChlamydiaScreening_CQM.cql"]

configurations {
    implementation {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr  group: "org.antlr", name: "antlr4", version: "4.9.1"
    implementation (group: "org.antlr", name: "antlr4-runtime", version: "4.9.1") {
        // antlr 4.5 includes these classes directly
        exclude(group: "org.abego.treelayout", module: "org.abego.treelayout.core")
    }
}

ext.antlr = [
        package: 'org.cqframework.cql.gen',
        destination: 'src/generated/java'
]

sourceSets.main.antlr.srcDirs = ['../../grammar']
sourceSets.main.java.srcDirs += antlr.destination

generateGrammarSource {
    arguments = ['-visitor', '-package', antlr.package]
    outputDirectory = file("${antlr.destination}/${antlr.package.replaceAll("\\.", "/")}")
}

sourcesJar.dependsOn generateGrammarSource

clean {
    delete antlr.destination
}
