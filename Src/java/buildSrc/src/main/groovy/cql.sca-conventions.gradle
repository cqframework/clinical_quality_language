plugins {
    id 'java'
    id 'pmd'
    id 'com.github.spotbugs'
}

ext {
    pmdVersion = '6.55.0'
    spotBugsVersion = '4.7.3'
}

/*
    Turn on static code analysis by passing 'sca' as a system property:
    e.g., gradle -Dsca build

    Or add it to your user gradle.properties file (${HOME}/.gradle/gradle.properties):
    systemProp.sca=true
    */
if (System.getProperty('sca') != null) {

    dependencies {
        pmd(
            "net.sourceforge.pmd:pmd-core:${pmdVersion}",
            "net.sourceforge.pmd:pmd-java:${pmdVersion}"
        )
    }

    pmd {
        consoleOutput = true
        toolVersion = pmdVersion
        ruleSetFiles = files("${rootProject.projectDir}/config/custom-pmd-ruleset.xml")
        ruleSets = []
    }

    spotbugs {
        toolVersion = spotBugsVersion
        excludeFilter = file("${rootProject.projectDir}/config/findbugs-exclude-filter.xml")
    }

    spotbugsMain {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = true
            }
        }
    }
}
