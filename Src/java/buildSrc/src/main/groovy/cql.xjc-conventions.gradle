plugins {
    id 'java'
}

configurations {
    xjc
}

dependencies {
    xjc 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.13.1'
    xjc 'org.jvnet.jaxb2_commons:jaxb2-basics:0.13.1'
    xjc 'org.jvnet.jaxb2_commons:jaxb2-fluent-api:3.0'
    // Eclipse has taken over all Java EE reference components
    // https://www.infoworld.com/article/3310042/eclipse-takes-over-all-java-ee-reference-components.html
    // https://wiki.eclipse.org/Jakarta_EE_Maven_Coordinates
    xjc 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
    xjc 'org.glassfish.jaxb:jaxb-xjc:2.4.0-b180830.0438'
    xjc 'org.eclipse.persistence:org.eclipse.persistence.moxy:2.7.7'
    xjc 'org.slf4j:slf4j-simple:1.7.36'

    api 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:0.13.1'
    api 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
}

ext.xjc = [
    destDir: "${buildDir}/generated/sources/$name/main/java",
    args: '-disableXmlSecurity -Xfluent-api -Xequals -XhashCode -XtoString'
]


task generateSources {

    outputs.dir xjc.destDir

    doLast {
        file(xjc.destDir).mkdirs()

        ant.taskdef(name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task', classpath: configurations.xjc.asPath)

        /* The above sets up the task, but the real work of the task should be specified in the subproject using
            generateSources.doLast.  For example:
                generateSources.doLast {
                    ant.xjc(destdir: xjc.destDir, schema: "${projectDir}/path/to/file.xsd") {
                        arg(line: xjc.args)
                    }
                }
        */
    }
}

compileJava {
    dependsOn generateSources
}

sourcesJar {
    dependsOn generateSources
}

sourceSets {
    main {
        java {
            srcDir(xjc.destDir)
        }
    }
}

clean {
    delete xjc.destDir
}