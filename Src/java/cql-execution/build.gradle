import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'coffeescript-base'

group = 'org.cqframework'
version = '0.1'
sourceCompatibility = '1.7'

repositories {
  mavenCentral()
  ivy {
    url "https://raw.githubusercontent.com/jashkenas/coffeescript/"
    layout "pattern", {
        artifact "[revision]/extras/[module].[ext]"
    }
  }
}

ext.coffee = [
    srcDir:  "${projectDir}/../../coffeescript/cql-execution/src",
    destDir: "${projectDir}/src/main/resources/org/cqframework/cql/execution/javascript"
]

configurations {
    coffeescript
}

dependencies {
    coffeescript group: 'coffee-script', name: 'coffee-script', version: '1.8.0', ext: 'js'
    compile project(':cql-to-elm')
    compile group: 'org.mozilla', name: 'rhino', version: '1.7R4'
    testCompile group: 'com.fasterxml.jackson.core', name:'jackson-core', version:'2.4.1'
    testCompile group: 'com.fasterxml.jackson.core', name:'jackson-databind', version:'2.4.1'
    testCompile group: 'org.testng', name: 'testng', version: '6.8.8'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

task compileCoffee(type: CoffeeScriptCompile) {
    setCoffeeScriptJs(configurations.coffeescript)
    source fileTree(coffee.srcDir).include('**/*.coffee')
    destinationDir file(coffee.destDir)
}

compileJava.dependsOn compileCoffee

findbugs {
    toolVersion = "3.0.0"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

test {
    useTestNG()
}

clean {
    delete fileTree(coffee.destDir).include('cql*.js')
}