import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile

apply plugin: 'coffeescript-base'

repositories {
  ivy {
    url "https://raw.githubusercontent.com/jashkenas/coffeescript/"
    layout "pattern", {
        artifact "[revision]/extras/[module].[ext]"
    }
  }
}

ext.coffee = [
    srcDir:  "${projectDir}/../../coffeescript/cql-execution/src",
    destDir: "${projectDir}/src/main/resources/org/cqframework/cql/execution/javascript"
]

configurations {
    coffeescript
}

dependencies {
    coffeescript group: 'coffee-script', name: 'coffee-script', version: '1.9.2', ext: 'js'
    compile project(':cql-to-elm')
    compile group: 'org.mozilla', name: 'rhino', version: '1.7.6'
    testCompile group: 'com.fasterxml.jackson.core', name:'jackson-core', version:'2.4.1'
    testCompile group: 'com.fasterxml.jackson.core', name:'jackson-databind', version:'2.4.1'
}

task compileCoffee(type: CoffeeScriptCompile) {
    setCoffeeScriptJs(configurations.coffeescript)
    source fileTree(coffee.srcDir).include('**/*.coffee')
    destinationDir file(coffee.destDir)
}

compileJava.dependsOn compileCoffee

clean {
    delete fileTree(coffee.destDir).include('cql*.js')
}

generateSources {
    inputs.dir "${projectDir}/../../cql-lm/schema"

    doLast {
        ant.xjc(destdir: xjc.destDir, schema: "${projectDir}/../../cql-lm/schema/elm/library.xsd") {
            arg(line: "${xjc.args} -npa -XautoInheritance -XautoInheritance-xmlTypesExtend=org.cqframework.cql.elm.execution.Executable")
        }
    }
}