apply plugin: 'application'

mainClassName = 'org.cqframework.cql.cql2elm.CqlTranslator'
run.args = ["--input", "${projectDir}/../../../Examples/CMS146v2_CQM.cql"]

dependencies {
    api project(':cql')
    api project(':model')
    api project(':elm')

    // The FHIR ucum service depends on an
    // XML pull-parser being present on the classpath, and have a compile-time reference to xpp3.
    // However, some platfoms such as Android provide their own pull-parser implementations.
    // Additionally, The xpp3.xpp3 dependency includes the javax.xml packagename, which is invalid in
    // Java 9+ because split packages (i.e. the same packagename being available in multipe jar) are
    // disallowed. For those reasons xpp3 is excluded here, and included as a runtime dependency for
    // tests. Users of the CQL translator will need to supply their own implementation or add xpp3
    // at runtime.
    api ('org.fhir:ucum:1.0.3') {
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: '2.13.2'

    testImplementation project(':elm-jackson')
    testImplementation project(':model-jackson')
    testImplementation project(':quick')
    testImplementation project(':qdm')
    testImplementation group: 'com.github.reinert', name: 'jjschema', version: '1.16'
    testRuntimeOnly group: 'xpp3', name: 'xpp3', version: '1.1.4c'
}